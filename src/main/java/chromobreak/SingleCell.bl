package chromobreak

import chromobreak.SingleCellHMMs
import chromobreak.SingleCellHMMs.Configs
import blang.types.internals.RealScalar

model SingleCell {
  param SingleCellData data
  param Configs configs
  
  param String chromosomeFilter ?: ".*"
  param Double vagueness ?: 10.0
  
  random RealVar switchRate ?: latentReal
  random RealVar f0 ?: latentReal
  random RealVar f1 ?: latentReal 
  random RealVar f2 ?: latentReal  
  random RealVar sd ?: latentReal 
  random RealVar p ?: new RealScalar(0.5) 
   
  random ReadCountModel readCountModel ?: new ReadCountModel(f0, f1, f2, sd, 0.0)
  
  // Keeping a copy to easily serialize demarginalized hmms
  random Map<String, SingleCellHMMs> hmms ?: new LinkedHashMap
    
  laws {
    
    switchRate ~ Exponential(1.0)
    
    f0 | vagueness ~ Normal(0, vagueness*vagueness) 
    f1 | vagueness ~ Normal(0, vagueness*vagueness)  
    f2 | vagueness ~ Normal(0, vagueness*vagueness)  
    
    sd ~ Exponential(0.1)
    
    p ~ ContinuousUniform(0.0, 1.0)
    
    for (Index<String> chromosome : data.chromosomes.indices.filter[key.matches("^" + chromosomeFilter + "$")]) {  
      | SingleCellHMMs hmm = {
          val current = new SingleCellHMMs(
            data, 
            chromosome, 
            readCountModel, 
            switchRate, 
            configs,
            configs.maxStates
          )
          hmms.put(chromosome.key, current)
          current
      } 
      ~ LogPotential(hmm.logMarginal) 
    }
  }
}