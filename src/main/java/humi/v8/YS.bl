package humi.v8
import humi.HumiData
import humi.Monitor
import java.util.function.Supplier

model YS { 
  param HumiData data
  param Plated<IntVar> initialPopCounts
  random Plated<RealVar> rhos
  random Plated<RealVar> lambdas
  random RealVar shape ?: latentReal, rate ?: latentReal
  random Plated<Monitor> visibleCloneNumbers, truncatedMeans, winsorizedMeans, medians
  param RealVar vagueRate ?: 0.01
  
  laws {
    shape | vagueRate ~ Exponential(vagueRate)
    rate | vagueRate ~ Exponential(vagueRate)
    for (Index<Integer> target : data.targets.indices) {
      lambdas.get(target) | shape, rate ~ Gamma(shape, rate)
    }
    for (Index<Integer> target : data.targets.indices) {
      rhos.get(target) | vagueRate ~ Exponential(vagueRate)
      for (Index<String> experiment : data.experiments.indices(target)) {
        data.histograms.get(target, experiment) |
          RealVar lambda = lambdas.get(target), 
          IntVar initialPopCount = initialPopCounts.get(target),
          Supplier<IntDistribution> dist = {
            val rho = rhos.get(target)
            val Supplier<IntDistribution> result = [YuleSimon::distribution(rho)]
            val lambda = lambdas.get(target)
            DistributionSummary::registerMonitors(
              visibleCloneNumbers, truncatedMeans, winsorizedMeans, medians, target, result, 
              initialPopCounts, lambda, data, experiment
            )
            return result
          }
        ~ CensoredExchangeableCounts(dist.get, initialPopCount * lambda)   
      }   
    }
  }
}