package humi.v5

import humi.HumiData
import humi.IntMixture

model M_7 { 
  param HumiData data
  param Plated<IntVar> initialPopCounts
  random Plated<RealVar> rhos
    
  random RealVar rhoGlobal ?: latentReal
  random Plated<RealVar> lambdas, pis
  
  param RealVar vagueRate ?: 0.01
  
  laws {
    
    for (Index<String> experiment : data.experiments.indices) {
      lambdas.get(experiment) | vagueRate ~ Exponential(vagueRate)
    }
    
    rhoGlobal | vagueRate ~ Exponential(vagueRate)
    
    for (Index<Integer> target : data.targets.indices) {
      
      rhos.get(target) | vagueRate ~ Exponential(vagueRate)
      pis.get(target) ~ ContinuousUniform(0.0, 1.0) 
       
      for (Index<String> experiment : data.experiments.indices(target)) {
        // final counts likelihood
        data.histograms.get(target, experiment) |
          RealVar rho = rhos.get(target),
          rhoGlobal,
          RealVar pi = pis.get(target),
          RealVar lambda = lambdas.get(experiment),
          IntVar initialPopCount = initialPopCounts.get(target), 
          double nExperiments = data.experiments.indices.size
        ~ CensoredExchangeableCounts(
          {
            if (pi < 0.0 || pi > 1.0) invalidParameter
            IntMixture::distribution(
              fixedSimplex(pi, 1.0 - pi), 
              #[ // Warning: if edited, need to change M_5_Postprocess
                YuleSimon::distribution(rho), 
                YuleSimon::distribution(rhoGlobal)
              ]
            )
          }, 
          initialPopCount * lambda
        )   
      }   
    }
  }
}