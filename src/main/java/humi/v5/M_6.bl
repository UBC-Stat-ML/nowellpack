package humi.v5

import humi.HumiData
import humi.IntMixture

model M_6 { 
  param HumiData data
  param Plated<IntVar> initialPopCounts
  random Plated<RealVar> rho1s, rho2s, pis
    
  random Plated<RealVar> lambdas
  
  param RealVar vagueRate ?: 0.01
  
  laws {
    
    for (Index<String> experiment : data.experiments.indices) {
      lambdas.get(experiment) | vagueRate ~ Exponential(vagueRate)
    }
    
    for (Index<Integer> target : data.targets.indices) {
      
      pis.get(target) ~ ContinuousUniform(0.0, 1.0) 
      rho1s.get(target) | vagueRate ~ Exponential(vagueRate)
      rho2s.get(target) | vagueRate ~ Exponential(vagueRate)
       
      for (Index<String> experiment : data.experiments.indices(target)) {
        // final counts likelihood
        data.histograms.get(target, experiment) |
          RealVar rho1 = rho1s.get(target),
          RealVar rho2 = rho2s.get(target),
          RealVar pi = pis.get(target),
          RealVar lambda = lambdas.get(experiment),
          IntVar initialPopCount = initialPopCounts.get(target), 
          double nExperiments = data.experiments.indices.size
        ~ CensoredExchangeableCounts(
          {
            if (pi < 0.0 || pi > 1.0) invalidParameter
            IntMixture::distribution(
              fixedSimplex(0.5, 0.5), //pi, 1.0 - pi), 
              #[ // Warning: if edited, need to change M_5_Postprocess
                YuleSimon::distribution(rho1), 
                YuleSimon::distribution(rho1 + rho2)
              ]
            )
          }, 
          initialPopCount * lambda
        )   
      }   
    }
  }
}