package corrupt

import static extension corrupt.CorruptExtensionUtils.*
import static corrupt.CorruptStaticUtils.*

model Synthetic {
  param GlobalDataSource data
  param Plate<Cell> cells
  param Plate<Locus> loci
  
  param RealVar stdDev ?: 0.3
  random PerfectPhylo phylo ?: new PerfectPhylo(cells.set, loci.set)
  random Plated<RealVar> observations
  
  laws {
    phylo ~ Uniform
    
    for (Index<Cell> cell : cells.indices) {
      for (Index<Locus> locus : loci.indices(cell)) {
        observations.get(cell, locus) | 
          TipIndicator tipIndicator = phylo.tipIndicator(cell.key, locus.key),
          stdDev 
          ~ Normal(if (tipIndicator.included) 1 else 0, pow(stdDev, 2)) 
      }
    }
  }
}